!yamlscript/v0

defn mdys-fmt-fns(o):
  str "\n":
    fmt-fns: o

defn mdys-stdlib(o):
  str "\n":
    reduce-kv _ '' o:
      fn(md head body):
        md +:
          str "## $head\n\n":
            if body:map?:
              fmt-fns: body
              str:
                fmt-fns(body.0)
                "\n**See also**:\ <$(body.1)>\n\n"

defn fmt-fns(body):
  reduce-kv _ '' body:
    fn(md fn-sig desc):
      desc =:
        desc.has?(\\|).if("$desc\n" desc)
        .replace(/\b(C[CDS])\/(\S+)/ clojure-docs-link)
      md +: |
        * `$fn-sig` â€” $(desc.replace(/\| / "\n\n    "))

defn clojure-docs-link([_ type name]):
  func =: name.replace(/\?$/ '_q')
  cond:
    type == 'CC':
      "[`clojure.core/$name`](https://clojuredocs.org/clojure.core/$func)"
    type == 'CD':
      "[`clojure.data/$name`](https://clojuredocs.org/clojure.data/$func)"
    type == 'CS':
      "[`clojure.string/$name`](https://clojuredocs.org/clojure.string/$func)"

defn mdys-quick-ref(o):
  links =:
    join ', ':
      for name words(o):
        "[$name](https://clojuredocs.org/clojure.core/$name)"

  =>: |+
    Clojure Core:

    * $links

# vim: ft=yaml sw=2 lisp:
